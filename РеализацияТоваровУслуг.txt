

&После("ПередЗаписью")
Процедура RoistatПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ТекущийКонтрагент = ЭтотОбъект.Контрагент;
	
	Если ЗначениеЗаполнено(ТекущийКонтрагент) И ТекущийКонтрагент.ПометкаУдаления = Истина Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ
		|	И Контрагенты.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("ИНН", ТекущийКонтрагент.ИНН);
		Запрос.УстановитьПараметр("КПП", ТекущийКонтрагент.КПП);
		Запрос.УстановитьПараметр("Ссылка", ТекущийКонтрагент.Ссылка);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			//создаем нового контрагента
			НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
			//заполняем всеми необходимыми данными из помеченного на удаление контрагента
			ЗаполнитьЗначенияСвойств(НовыйКонтрагент, ТекущийКонтрагент,, "Ссылка, ПометкаУдаления,Код, Владелец");
			
			//заполняем ТЧ контактные данные
			Для Каждого Стр из ТекущийКонтрагент.КонтактнаяИнформация Цикл
				Нстр = НовыйКонтрагент.КонтактнаяИнформация.Добавить();
				ЗаполнитьЗначенияСвойств(Нстр, Стр);
			КонецЦикла;
			//Заполняем ТЧ ДополнительныеРеквизиты
			Для Каждого Стр из ТекущийКонтрагент.ДополнительныеРеквизиты Цикл
				Нстр = НовыйКонтрагент.ДополнительныеРеквизиты.Добавить();
				ЗаполнитьЗначенияСвойств(Нстр, Стр);
			КонецЦикла;
			//записываем нового контрагента, заменяем ссылку в документе на него
			НовыйКонтрагент.Записать();
			ЭтотОбъект.Контрагент = НовыйКонтрагент.Ссылка;
		Иначе
			ЭтотОбъект.Контрагент = Результат.Выгрузить()[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийДоговор = ЭтотОбъект.ДоговорКонтрагента;
	Если ЗначениеЗаполнено(ТекущийДоговор) И ТекущийДоговор.ПометкаУдаления = Истина Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Наименование = &Наименование
		|	И ДоговорыКонтрагентов.Ссылка <> &Ссылка
		|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("Наименование", ТекущийДоговор.Наименование);
		Запрос.УстановитьПараметр("Ссылка", ТекущийДоговор.Ссылка);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			//создаем нового контрагента
			НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
			НовыйДоговор.Дата = ТекущаяДатаСеанса(); 
			НовыйДоговор.Владелец = ТекущийКонтрагент;
			ЗаполнитьЗначенияСвойств(НовыйДоговор, ТекущийДоговор,,"Ссылка, ПометкаУдаления,Код, Номер, Дата, ДатаОплаты, Владелец");
			//заполняем ТЧ ДополнительныеРеквизиты
			Для Каждого Стр из ТекущийДоговор.ДополнительныеРеквизиты Цикл
				Нстр = НовыйДоговор.ДополнительныеРеквизиты.Добавить();
				ЗаполнитьЗначенияСвойств(Нстр, Стр);
			КонецЦикла;
			//Заполняем ТЧ ДополнительныеСоглашения
			Для Каждого Стр из ТекущийДоговор.ДополнительныеСоглашения Цикл
				Нстр = НовыйДоговор.ДополнительныеСоглашения.Добавить();
				ЗаполнитьЗначенияСвойств(Нстр, Стр);
			КонецЦикла;
			//записываем нового контрагента, заменяем ссылку в документе на него
			НовыйДоговор.Записать();
			ЭтотОбъект.ДоговорКонтрагента = НовыйДоговор.Ссылка;
		Иначе
			ЭтотОбъект.ДоговорКонтрагента = Результат.Выгрузить()[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		Если ЭтотОбъект.ДоговорКонтрагента.ПометкаУдаления = Истина ИЛИ ЭтотОбъект.Контрагент.ПометкаУдаления = Истина Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
